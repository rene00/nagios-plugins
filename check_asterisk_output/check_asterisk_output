#!/usr/bin/perl -w
# Plugin to check output of asterisk command.

use strict;
use File::Basename;
use Nagios::Plugin;

$|=1;

my $VERSION = '0.1';
my %options = ();
my $bin_asterisk = '/usr/sbin/asterisk';
my $process = 'asterisk';
my $count = 0;
my (@warning,@critical,$errors_only);
my ($asterisk_cmd, $search);

my $np = Nagios::Plugin->new(
	version => '0.1',
	usage => "usage: $0  -a [asterisk command] -s [search] -w [warning] -c [critical]",
	extra => "
example: $0 -a 'sip show peers' -s '^route\\s*77\\.75\\.1\\.71' -w 0,2 -c 0,3

Return CRITICAL if
o the asterisk command 'sip show peers' shows '^route\\s*77\\.75\\.1\\.71' 0 or >=3 times.

Return WARNING if
o the asterisk command 'sip show peers' shows '^route\\s*77\\.75\\.1\\.71' 0 or >=2 times.

* Sat Jun 23 2012 Rene Cunningham <rene\@compounddata.com>
"
);

$np->add_arg(
	spec => 'asterisk_cmd|a=s',
	help => '-a, --asteriskcmd=STRING',
	required => 1,
);

$np->add_arg(
	spec => 'search|s=s',
	help => '-s, --search=STRING',
	required => 1,
);

$np->add_arg(
	spec => 'warning|w=s',
	help => '-w, --warning=WARNING',
	required => 1,
);

$np->add_arg(
	spec => 'critical|c=s',
	help => '-c, --critical=critical',
	required => 1,
);


$np->getopts;

($asterisk_cmd, $search) = (
		scalar($np->opts->asterisk_cmd), 
		scalar($np->opts->search)
);

@warning = split(',',$np->opts->warning);
@critical = split(',',$np->opts->critical);

# Exit with an error if we're not on Linux
$np->nagios_exit("UNKNOWN", "only works on Linux") unless $^O eq 'linux';

# exit if cant find /usr/sbin/asterisk
$np->nagios_exit("UNKNOWN", "cant find $bin_asterisk") if not -x $bin_asterisk;

open(PIPE,"sudo $bin_asterisk -rx \'$asterisk_cmd\' 2>&1|") or
	$np->nagios_exit("UNKNOWN","Failed to run \'$bin_asterisk -rx  $asterisk_cmd\' : $!\n");

while (my $line = <PIPE>) {
    chomp($line);
    if($line =~ /$search/) {
		$count++;
    }
}
close(PIPE);

if($count <= $critical[0]) {
    $np->nagios_exit("CRITICAL","\'$search\' found $count or less times in asterisk command \'$asterisk_cmd\' (<= $critical[0])\n");
} elsif($count <= $warning[0]) {
    $np->nagios_exit("WARNING","\'$search\' found $count or less times in asterisk command \'$asterisk_cmd\' (<= $warning[0])\n");
} 

if($count >= $critical[1]) {
    $np->nagios_exit("CRITICAL","\'$search\' found $count or more times in asterisk command \'$asterisk_cmd\' (>= $critical[1])\n");
} elsif($count >= $warning[1]) {
    $np->nagios_exit("WARNING","\'$search\' found $count or more times in asterisk command \'$asterisk_cmd\' (>= $warning[1])\n");
} else {
    $np->nagios_exit("OK","\'$search\' found $count time(s) in asterisk command \'$asterisk_cmd\'\n");
}
